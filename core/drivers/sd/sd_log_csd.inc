;-----------------------------------------------------------------------------------------------------------------------
;Файл распространяется под лицензией GPL-3.0-or-later, https://www.gnu.org/licenses/gpl-3.0.txt
;-----------------------------------------------------------------------------------------------------------------------
;27.01.2021	w5277c@gmail.com			Начало
;05.07.2021	w5277c@gmail.com			Z->Y
;-----------------------------------------------------------------------------------------------------------------------
.ifdef DEF_C5_SD_LOG_CSD
.else
.set DEF_C5_SD_LOG_CSD = 1
.ifdef LOGGING_PORT

.include	"./core/log/log_byte.inc"
.include	"./core/log/log_numx32.inc"
.include	"./core/log/log_numx16.inc"
.include	"./core/log/log_numx8.inc"
.include	"./core/log/log_cr.inc"
.include	"./core/log/log_char.inc"
.include	"./core/log/log_str_el.inc"
.include	"./core/log/log_str.inc"
.include	"./core/log/log_strn.inc"
.include	"./core/log/log_bytes.inc"


_DRV_SD_LOG_STR_CSD:
	.db "CSD register:",0x0d,0x0a,0x00
_DRV_SD_LOG_STR_CSD_STRUCTURE:
	.db " CSD_STRUCTURE:",0x00
_DRV_SD_LOG_STR_CSD_TAAC:
	.db " TAAC:",0x00
_DRV_SD_LOG_STR_CSD_NSAC:
	.db " NSAC(x100clck):",0x00
_DRV_SD_LOG_STR_CSD_VALUES:
	.db "rsv",0x00,"1.0",0x00,"1.2",0x00,"1.3",0x00,"1.5",0x00,"2.0",0x00,"2.5",0x00,"3.0",0x00,"3.5",0x00,"4.0",0x00,"4.5",0x00,"5.0",0x00,"5.5",0x00,"6.0",0x00,"7.0",0x00,"8.0",0x00
_DRV_SD_LOG_STR_CSD_TIMES:
	.db "1ns",0x00,"10ns",0x00,"100ns",0x00,"1us",0x00,"10us",0x00,"100us",0x00,"1ms",0x00,"10ms",0x00
_DRV_SD_LOG_STR_CSD_TRAN_SPEED:
	.db " TRAN_SPEED:",0x00
_DRV_SD_LOG_STR_CSD_RATES:
	.db "100Kbit/s",0x00,"1Mbit/s",0x00,"10Mbit/s",0x00,"100Mbit/s",0x00,"rsv",0x00,"rsv",0x00,"rsv",0x00,"rsv",0x00
_DRV_SD_LOG_STR_CSD_CCC:
	.db " CCC:",0x0d,0x0a,0x00
_DRV_SD_LOG_STR_CSD_CLASS:
	.db "  class ",0x00
_DRV_SD_LOG_STR_CSD_READ_BL_LEN:
	.db " READ_BL_LEN:",0x00
_DRV_SD_LOG_STR_CSD_LENS:
	.db "rsv",0x00,"rsv",0x00,"rsv",0x00,"rsv",0x00,"rsv",0x00,"rsv",0x00,"rsv",0x00,"rsv",0x00,"rsv",0x00,"512 Bytes",0x00,"1024 Bytes",0x00,"2048 Bytes",0x00,"rsv",0x00,"rsv",0x00,"rsv",0x00,"rsv",0x00
_DRV_SD_LOG_STR_CSD_READ_BL_PARTIAL:
	.db " READ_BL_PARTIAL:",0x00
_DRV_SD_LOG_STR_CSD_WRITE_BLK_MISALIGN:
	.db " WRITE_BLK_MISALIGN:",0x00
_DRV_SD_LOG_STR_CSD_READ_BLK_MISALIGN:
	.db " READ_BLK_MISALIGN:",0x00
_DRV_SD_LOG_STR_CSD_DSR_IMP:
	.db " READ_DSR_IMP:",0x00
_DRV_SD_LOG_STR_CSD_C_SIZE:
	.db " C_SIZE:",0x00
_DRV_SD_LOG_STR_CSD_VDD_R_CURR_MIN:
	.db " VDD_R_CURR_MIN:",0x00
_DRV_SD_LOG_STR_CSD_CURRENTS_MIN:
	.db "0.5mA",0x00,"1mA",0x00,"5mA",0x00,"10mA",0x00,"25mA",0x00,"35mA",0x00,"60mA",0x00,"100mA",0x00
_DRV_SD_LOG_STR_CSD_VDD_R_CURR_MAX:
	.db " VDD_R_CURR_MAX:",0x00
_DRV_SD_LOG_STR_CSD_CURRENTS_MAX:
	.db "1mA",0x00,"5mA",0x00,"10mA",0x00,"25mA",0x00,"35mA",0x00,"45mA",0x00,"80mA",0x00,"200mA",0x00
_DRV_SD_LOG_STR_CSD_VDD_W_CURR_MIN:
	.db " VDD_W_CURR_MIN:",0x00
_DRV_SD_LOG_STR_CSD_VDD_W_CURR_MAX:
	.db " VDD_W_CURR_MAX:",0x00
_DRV_SD_LOG_STR_CSD_C_SIZE_MULT:
	.db " C_SIZE_MULT:",0x00
_DRV_SD_LOG_STR_CSD_ERASE_BLR_EN:
	.db " ERASE_BLR_EN:",0x00
_DRV_SD_LOG_STR_CSD_SECTOR_SIZE:
	.db " SECTOR_SIZE(1-128 blk):",0x00
_DRV_SD_LOG_STR_CSD_WP_GRP_SIZE:
	.db " WP_GRP_SIZE(1-128 sec):",0x00
_DRV_SD_LOG_STR_CSD_WP_GRP_ENABLE:
	.db " WP_GRP_ENABLE:",0x00
_DRV_SD_LOG_STR_CSD_R2W_FACTOR:
	.db " R2W_FACTOR:",0x00
_DRV_SD_LOG_STR_CSD_WRITE_BL_LEN:
	.db " WRITE_BL_LEN:",0x00
_DRV_SD_LOG_STR_CSD_WRITE_BL_PARTIAL:
	.db " WRITE_BL_PARTIAL:",0x00
_DRV_SD_LOG_STR_CSD_FILE_FORMAT_GRP:
	.db " FILE_FORMAT_GRP:",0x00
_DRV_SD_LOG_STR_CSD_COPY:
	.db " COPY:",0x00
_DRV_SD_LOG_STR_CSD_PERM_WRITE_PROTECT:
	.db " PERM_WRITE_PROTECT:",0x00
_DRV_SD_LOG_STR_CSD_TMP_WRITE_PROTECT:
	.db " TMP_WRITE_PROTECT:",0x00
_DRV_SD_LOG_STR_CSD_FILE_FORMAT:
	.db " FILE_FORMAT:",0x00
_DRV_SD_LOG_STR_CSD_FILE_FORMATS:
	.db " Hard disk-like FS with PT",0x00,"DOS FAT",0x00,"Universal",0x00,"Others",0x00
_DRV_SD_LOG_STR_CSD_RSV:
	.db "rsv",0x00

;--------------------------------------------------------
DRV_SD_LOG_CSD:
;Логирование CSD
;IN: X-адрес на блок данных
;--------------------------------------------------------
	PUSH_Z
	PUSH_Y
	PUSH TEMP
	PUSH TEMP_L
	PUSH TEMP_H
	PUSH ACCUM

	MOVW YL,XL

	LDI_Z _DRV_SD_LOG_STR_CSD|0x8000
	MCALL C5_LOG_STR

	LDD TEMP_L,Y+0x00
	LDI_Z _DRV_SD_LOG_STR_CSD_STRUCTURE|0x8000
	MCALL C5_LOG_STR
	MOV TEMP,TEMP_L
	SWAP TEMP
	LSR TEMP
	LSR TEMP
	MCALL C5_LOG_BYTE
	MCALL C5_LOG_CR
	;CSD ver
	MOV ACCUM,TEMP

	LDD TEMP_L,Y+0x01
	LDI_Z _DRV_SD_LOG_STR_CSD_TAAC|0x8000
	MCALL C5_LOG_STR
	MOV TEMP,TEMP_L
	LSL TEMP
	SWAP TEMP
	ANDI TEMP,0x0f
	LDI_Z _DRV_SD_LOG_STR_CSD_VALUES|0x8000
	MCALL C5_LOG_STR_EL
	LDI TEMP,'x'
	MCALL C5_LOG_CHAR
	MOV TEMP,TEMP_L
	ANDI TEMP,0x07
	LDI_Z _DRV_SD_LOG_STR_CSD_TIMES|0x8000
	MCALL C5_LOG_STR_EL
	MCALL C5_LOG_CR

	LDD TEMP,Y+0x02
	LDI_Z _DRV_SD_LOG_STR_CSD_NSAC|0x8000
	MCALL C5_LOG_STR
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	LDD TEMP_L,Y+0x03
	LDI_Z _DRV_SD_LOG_STR_CSD_TRAN_SPEED|0x8000
	MCALL C5_LOG_STR
	MOV TEMP,TEMP_L
	LSL TEMP
	SWAP TEMP
	ANDI TEMP,0x0f
	LDI_Z _DRV_SD_LOG_STR_CSD_VALUES|0x8000
	MCALL C5_LOG_STR_EL
	LDI TEMP,'x'
	MCALL C5_LOG_CHAR
	MOV TEMP,TEMP_L
	ANDI TEMP,0x07
	LDI_Z _DRV_SD_LOG_STR_CSD_RATES|0x8000
	MCALL C5_LOG_STR_EL
	MCALL C5_LOG_CR

	LDD TEMP_H,Y+0x04
	LDD TEMP_L,Y+0x05
	LDI_Z _DRV_SD_LOG_STR_CSD_CCC|0x8000
	MCALL C5_LOG_STR
	SWAP TEMP_L
	SWAP TEMP_H
	ANDI TEMP_L,0x0f
	MOV TEMP,TEMP_H
	ANDI TEMP_H,0x0f
	ANDI TEMP,0xf0
	OR TEMP_L,TEMP

	LDI TEMP,0x00
	LDI_Z _DRV_SD_LOG_STR_CSD_CLASS|0x8000
_DRV_SD_LOG_CSD__CLASS_LOOP:
	LSR TEMP_H
	ROR TEMP_L
	BRCC PC+0x07
	MCALL C5_LOG_STR
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR
	INC TEMP
	CPI TEMP,0x0c
	BRNE _DRV_SD_LOG_CSD__CLASS_LOOP

	LDI_Z _DRV_SD_LOG_STR_CSD_READ_BL_LEN|0x8000
	MCALL C5_LOG_STR
	LDD TEMP,Y+0x05
	ANDI TEMP,0x0f
	LDI_Z _DRV_SD_LOG_STR_CSD_LENS|0x8000
	MCALL C5_LOG_STR_EL
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_READ_BL_PARTIAL|0x8000
	MCALL C5_LOG_STR
	LDD TEMP_L,Y+0x06
	LDI TEMP,0x00
	SBRC TEMP_L,0x07
	LDI TEMP,0x01
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_WRITE_BLK_MISALIGN|0x8000
	MCALL C5_LOG_STR
	LDI TEMP,0x00
	SBRC TEMP_L,0x06
	LDI TEMP,0x01
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_READ_BLK_MISALIGN|0x8000
	MCALL C5_LOG_STR
	LDI TEMP,0x00
	SBRC TEMP_L,0x05
	LDI TEMP,0x01
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_DSR_IMP|0x8000
	MCALL C5_LOG_STR
	LDI TEMP,0x00
	SBRC TEMP_L,0x04
	LDI TEMP,0x01
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	CPI ACCUM,0x00
	BRNE PC+0x04
	MCALL _DRV_SD_LOG_CSD_PART_V1
	RJMP PC+0x08
	CPI ACCUM,0x01
	BRNE PC+0x04
	MCALL _DRV_SD_LOG_CSD_PART_V2
	RJMP PC+0x03
	MCALL _DRV_SD_LOG_CSD_PART_V3

	LDI_Z _DRV_SD_LOG_STR_CSD_ERASE_BLR_EN|0x8000
	MCALL C5_LOG_STR
	LDD TEMP_L,Y+0x0a
	LDI TEMP,0x00
	SBRC TEMP_L,0x06
	LDI TEMP,0x01
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_SECTOR_SIZE|0x8000
	MCALL C5_LOG_STR
	LDD TEMP,Y+0x0a
	LDD TEMP_L,Y+0x0b
	LSL TEMP_L
	ROL TEMP
	ANDI TEMP,0x3f
	INC TEMP
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_WP_GRP_SIZE|0x8000
	MCALL C5_LOG_STR
	LDD TEMP,Y+0x0b
	ANDI TEMP,0x7f
	INC TEMP
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_WP_GRP_ENABLE|0x8000
	MCALL C5_LOG_STR
	LDD TEMP_L,Y+0x0c
	LDI TEMP,0x00
	SBRC TEMP_L,0x07
	LDI TEMP,0x01
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_R2W_FACTOR|0x8000
	MCALL C5_LOG_STR
	LDD TEMP,Y+0x0c
	LSR TEMP
	LSR TEMP
	ANDI TEMP,0x07
	CPI TEMP,0x06
	BRCS PC+0x06
	LDI_Z _DRV_SD_LOG_STR_CSD_RSV|0x8000
	MCALL C5_LOG_STR
	RJMP PC+0x05
	MCALL BITNUM_TO_NUM
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_WRITE_BL_LEN|0x8000
	MCALL C5_LOG_STR
	LDD TEMP,Y+0x0c
	LDD TEMP_L,Y+0x0d
	LSL TEMP_L
	ROL TEMP
	LSL TEMP_L
	ROL TEMP
	ANDI TEMP,0x0f
	LDI_Z _DRV_SD_LOG_STR_CSD_LENS|0x8000
	MCALL C5_LOG_STR_EL
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_WRITE_BL_PARTIAL|0x8000
	MCALL C5_LOG_STR
	LDI TEMP,0x00
	SBRC TEMP_L,0x07
	LDI TEMP,0x01
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_FILE_FORMAT_GRP|0x8000
	MCALL C5_LOG_STR
	LDD TEMP_L,Y+0x0e
	LDI TEMP,0x00
	SBRC TEMP_L,0x07
	LDI TEMP,0x01
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR
	MOV TEMP_H,TEMP

	LDI_Z _DRV_SD_LOG_STR_CSD_COPY|0x8000
	MCALL C5_LOG_STR
	LDI TEMP,0x00
	SBRC TEMP_L,0x06
	LDI TEMP,0x01
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_PERM_WRITE_PROTECT|0x8000
	MCALL C5_LOG_STR
	LDI TEMP,0x00
	SBRC TEMP_L,0x05
	LDI TEMP,0x01
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_TMP_WRITE_PROTECT|0x8000
	MCALL C5_LOG_STR
	LDI TEMP,0x00
	SBRC TEMP_L,0x04
	LDI TEMP,0x01
	MCALL C5_LOG_NUMX8
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_FILE_FORMAT|0x8000
	MCALL C5_LOG_STR
	CPI TEMP_H,0x01
	BRNE _DRV_SD_LOG_CSD__FILE_FORMAT_NO_RSV
	LDI_Z _DRV_SD_LOG_STR_CSD_RSV|0x8000
	MCALL C5_LOG_STR
	RJMP _DRV_SD_LOG_CSD__FILE_FORMAT_DONE
_DRV_SD_LOG_CSD__FILE_FORMAT_NO_RSV:
	LSR TEMP_L
	LSR TEMP_L
	ANDI TEMP_L,0x03
	MOV TEMP,TEMP_L
	LDI_Z _DRV_SD_LOG_STR_CSD_FILE_FORMATS|0x8000
	MCALL C5_LOG_STR_EL
_DRV_SD_LOG_CSD__FILE_FORMAT_DONE:
	MCALL C5_LOG_CR

	POP ACCUM
	POP TEMP_H
	POP TEMP_L
	POP TEMP
	POP_Y
	POP_Z
	RET


_DRV_SD_LOG_CSD_PART_V1:
	LDI_Z _DRV_SD_LOG_STR_CSD_C_SIZE|0x8000
	MCALL C5_LOG_STR
	LDD TEMP_H,Y+0x06
	LSL TEMP_H
	LSL TEMP_H
	ANDI TEMP_H,0x0c
	LDD TEMP_L,Y+0x07
	LDD TEMP,Y+0x08
	LSL TEMP
	ROL TEMP_L
	ROL TEMP_H
	LSL TEMP
	ROL TEMP_L
	ROL TEMP_H
	ANDI TEMP_H,0x0f
	MCALL C5_LOG_NUMX16
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_VDD_R_CURR_MIN|0x8000
	MCALL C5_LOG_STR
	LDD TEMP,Y+0x08
	LSR TEMP
	LSR TEMP
	LSR TEMP
	ANDI TEMP,0x07
	LDI_Z _DRV_SD_LOG_STR_CSD_CURRENTS_MIN|0x8000
	MCALL C5_LOG_STR_EL
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_VDD_R_CURR_MAX|0x8000
	MCALL C5_LOG_STR
	LDD TEMP,Y+0x08
	ANDI TEMP,0x07
	LDI_Z _DRV_SD_LOG_STR_CSD_CURRENTS_MAX|0x8000
	MCALL C5_LOG_STR_EL
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_VDD_W_CURR_MIN|0x8000
	MCALL C5_LOG_STR
	LDD TEMP,Y+0x09
	SWAP TEMP
	LSR TEMP
	ANDI TEMP,0x07
	LDI_Z _DRV_SD_LOG_STR_CSD_CURRENTS_MIN|0x8000
	MCALL C5_LOG_STR_EL
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_VDD_W_CURR_MAX|0x8000
	MCALL C5_LOG_STR
	LDD TEMP,Y+0x09
	LSR TEMP
	LSR TEMP
	ANDI TEMP,0x07
	LDI_Z _DRV_SD_LOG_STR_CSD_CURRENTS_MAX|0x8000
	MCALL C5_LOG_STR_EL
	MCALL C5_LOG_CR

	LDI_Z _DRV_SD_LOG_STR_CSD_C_SIZE_MULT|0x8000
	MCALL C5_LOG_STR
	LDD TEMP,Y+0x09
	LDD TEMP_L,Y+0x0a
	LSL TEMP_L
	ROL TEMP
	ANDI TEMP,0x07
	MCALL BITNUM_TO_NUM
	CLR TEMP_H
	MOV TEMP_L,TEMP
	LSL TEMP_L
	ROL TEMP_H
	LSL TEMP_L
	ROL TEMP_H
	MCALL C5_LOG_NUMX16
	MCALL C5_LOG_CR
	RET

_DRV_SD_LOG_CSD_PART_V2:
	LDI_Z _DRV_SD_LOG_STR_CSD_C_SIZE|0x8000
	MCALL C5_LOG_STR
	LDI TEMP_EH,0x00
	LDD TEMP_EL,Y+0x07
	ANDI TEMP_EL,0x3f
	LDD TEMP_H,Y+0x08
	LDD TEMP_L,Y+0x09
	MCALL C5_LOG_NUMX32
	MCALL C5_LOG_CR
	RET

_DRV_SD_LOG_CSD_PART_V3:
	LDI_Z _DRV_SD_LOG_STR_CSD_C_SIZE|0x8000
	MCALL C5_LOG_STR
	LDD TEMP_EH,Y+0x06
	ANDI TEMP_EH,0x0f
	LDD TEMP_EL,Y+0x07
	LDD TEMP_H,Y+0x08
	LDD TEMP_L,Y+0x09
	MCALL C5_LOG_NUMX32
	MCALL C5_LOG_CR
	RET
.endif
.endif
