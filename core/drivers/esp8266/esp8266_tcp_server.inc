;-----------------------------------------------------------------------------------------------------------------------
;Файл распространяется под лицензией GPL-3.0-or-later, https://www.gnu.org/licenses/gpl-3.0.txt
;-----------------------------------------------------------------------------------------------------------------------
;01.10.2021	w5277c@gmail.com			Код вынесен из esp8266.inc
;-----------------------------------------------------------------------------------------------------------------------

.ifdef DEF_DRIVER_ESP8266_TCP_SERVER
.else
.set DEF_DRIVER_ESP8266_TCP_SERVER = 1

_DRV_ESP8266_AT_SET_MULTIPLE_CONNS:		.db "AT+CIPMUX=1",0x0d,0x0a,0x00
_DRV_ESP8266_AT_SET_RECV_MODE:			.db "AT+CIPRECVMODE=0",0x0d,0x0a,0x00,0x00
_DRV_ESP8266_AT_CREATE_TCP_SERVER:		.db "AT+CIPSERVER=1,",0x00;+PORT+END
_DRV_ESP8266_AT_SET_SERVER_TIMEOUT:		.db "AT+CIPSTO=5",0x0d,0x0a,0x00

;--------------------------------------------------------
_DRV_ESP8266__OP_TCP_SERVER:
;--------------------------------------------------------
;Активируем TCP сервер
;IN; TEMP_H/L-TCP порт
;OUT:TEMP_H-RESULT
;--------------------------------------------------------
	PUSH_X
	PUSH_Y
	PUSH TEMP_EH
	PUSH TEMP_EL
	PUSH TEMP_L

	PUSH TEMP_H
	PUSH TEMP_L

	;SET MULTIPLE CONNECTIONS
	MOVW XL,YL
	ADIW XL,_DRV_ESP8266_BUFFER
	LDI_Z _DRV_ESP8266_AT_SET_MULTIPLE_CONNS|0x8000
	MCALL STR_TO_RAM
	MOVW ZL,YL
	ADIW ZL,_DRV_ESP8266_BUFFER
	SBIW XL,_DRV_ESP8266_BUFFER
	SUB XL,YL
	MOV TEMP_H,XL
	MCALL _DRV_ESP8266__OP_RAW
	MCALL _DRV_ESP8266_CHECK_RESP_OK
	CPI TEMP_H,DRV_RESULT_OK
	BREQ PC+0x02
	RJMP _DRV_ESP8266__OP_TCP_SERVER_ERROR1

	;SET RECIEVE MODE (CAN NOT SUPORTED)
	MOVW XL,YL
	ADIW XL,_DRV_ESP8266_BUFFER
	LDI_Z _DRV_ESP8266_AT_SET_RECV_MODE|0x8000
	MCALL STR_TO_RAM
	MOVW ZL,YL
	ADIW ZL,_DRV_ESP8266_BUFFER
	SBIW XL,_DRV_ESP8266_BUFFER
	SUB XL,YL
	MOV TEMP_H,XL
	MCALL _DRV_ESP8266__OP_RAW

	LDI XH,0x00
	MOVW XL,YL
	ADIW XL,_DRV_ESP8266_BUFFER
	LDI_Z _DRV_ESP8266_AT_CREATE_TCP_SERVER|0x8000
	MCALL STR_TO_RAM
	POP TEMP_L
	POP TEMP_H
	MOVW ZL,XL
	MCALL NUM16_TO_STR
	MOVW XL,ZL
	LDI_Z _DRV_ESP8266_AT_NEW_LINE|0x8000
	MCALL STR_TO_RAM
	MOVW ZL,YL
	ADIW ZL,_DRV_ESP8266_BUFFER
	SBIW XL,_DRV_ESP8266_BUFFER
	SUB XL,YL
	MOV TEMP_H,XL
	MCALL _DRV_ESP8266__OP_RAW
	MCALL _DRV_ESP8266_CHECK_RESP_OK
	CPI TEMP_H,DRV_RESULT_OK
	BRNE _DRV_ESP8266__OP_TCP_SERVER_ERROR2

	LDI XH,0x00
	MOVW XL,YL
	ADIW XL,_DRV_ESP8266_BUFFER
	LDI_Z _DRV_ESP8266_AT_SET_SERVER_TIMEOUT|0x8000
	MCALL STR_TO_RAM
	MOVW ZL,YL
	ADIW ZL,_DRV_ESP8266_BUFFER
	SBIW XL,_DRV_ESP8266_BUFFER
	SUB XL,YL
	MOV TEMP_H,XL
	MCALL _DRV_ESP8266__OP_RAW

	LDI TEMP_H,DRV_RESULT_OK
	RJMP _DRV_ESP8266__OP_TCP_SERVER_END
_DRV_ESP8266__OP_TCP_SERVER_ERROR1:
	POP TEMP_L
	POP TEMP_H
_DRV_ESP8266__OP_TCP_SERVER_ERROR2:
	LDI TEMP_H,DRV_RESULT_ERROR
_DRV_ESP8266__OP_TCP_SERVER_END:
	POP TEMP_L
	POP TEMP_EL
	POP TEMP_EH
	POP_Y
	POP_X
	RET
.endif
