;-----------------------------------------------------------------------------------------------------------------------
;Файл распространяется под лицензией GPL-3.0-or-later, https://www.gnu.org/licenses/gpl-3.0.txt
;-----------------------------------------------------------------------------------------------------------------------
;16.04.2021  w5277c@gmail.com			Начало
;-----------------------------------------------------------------------------------------------------------------------
.ifdef DEF__C5_DRV_HC05
.else
.set DEF__C5_DRV_HC05 = 1
.message "Included driver HC-05 v0.1"

.include	"./core/ram/ram_realloc.inc"
.include	"./core/ram/ram_offset.inc"
.include	"./str/str_to_ram.inc"
.include	"./str/str_find.inc"
.include	"./conv/num16_to_str.inc"
.include	"./core/time32_mark.inc"
.include	"./core/time32_delta.inc"
.include	"./prim/common.inc"
.include	"./prim/prim_mov.inc"
.include	"./prim/prim_cpi.inc"

.ifdef LOGGING_PORT
	.if LOGGING_LEVEL >= LOGGING_LVL_PNC
		.include	"./core/io/out_char.inc"
		.include	"./core/io/out_strn.inc"
		.include	"./str/strn_trim.inc"
		.include	"./core/io/out_cr.inc"
	.endif
.endif

;---CONSTANTS--------------------------------------------
	.EQU	_DRV_HC05_VARS_SIZE								= 0x0a
	;---VARS---
	.EQU	_DRV_HC05_BUFFER_SIZE							= 0xc0;Размер буфера
	;---OFFSETS---
	.EQU	_DRV_HC05_UART_DRV_ID							= 0x00;1Б - Идентификатор драйвера UART
	.EQU	_DRV_HC05_TIME_START								= 0x01;4Б - Переменная INT (4 байта)
	.EQU	_DRV_HC05_TIME_DELTA								= 0x05;4Б - Переменная INT (4 байта)
	.EQU	_DRV_HC05_FLAGS									= 0x09;1Б - Флаги драйвера
	.EQU	_DRV_HC05_BUFFER_OFFSET							= 0x0a;Адрес начала буфера в выделенной памяти
	;---FLAGS---
	.EQU	DRV_HC05_FLAG_LOGGING							= 0x00;Бит логирования

	;---OPERATIONS---
	.EQU	DRV_HC05_OP_INIT									= 0x00;Инициализация работы
	.EQU	DRV_HC05_OP_RAW									= 0x01;Свободный формат данных

	;---RESULTS---
	.EQU	DRV_HC05_RESULT_OK								= 0x00
	.EQU	DRV_HC05_RESULT_ERROR							= 0x01
	.EQU	DRV_HC05_RESULT_NO_ANSWER						= 0x02

	;---AT-CONSTANTS---
_DRV_HC05_AT_ECHO_OFF:					.db "ATE0",0x0d,0x0a,0x00,0x00
_DRV_HC05_AT_SET_UART1:					.db "AT+UART_CUR=",0x00,0x00
_DRV_HC05_AT_SET_UART2:					.db ",8,1,0,0",0x0d,0x0a,0x00,0x00
_DRV_HC05_AT_SET_WIFI1:					.db "AT+CWJAP_CUR=",0x22,0x00,0x00;+APN+
_DRV_HC05_AT_SET_WIFI2:					.db 0x22,",",0x22,0x00;+PASSWORD+
_DRV_HC05_AT_SET_WIFI3:					.db 0x22,0x0d,0x0a,0x00
_DRV_HC05_AT_GET_IP:						.db "AT+CIFSR",0x0d,0x0a,0x00,0x00
_DRV_HC05_AT_SET_MULTIPLE_CONNS:		.db "AT+CIPMUX=1",0x0d,0x0a,0x00
_DRV_HC05_AT_RES_MULTIPLE_CONNS:		.db "AT+CIPMUX=0",0x0d,0x0a,0x00
_DRV_HC05_AT_SET_SERVER_TIMEOUT:		.db "AT+CIPSTO=5",0x0d,0x0a,0x00
_DRV_HC05_AT_SET_RECV_MODE:			.db "AT+CIPRECVMODE=0",0x0d,0x0a,0x00,0x00
_DRV_HC05_AT_CREATE_TCP_SERVER:		.db "AT+CIPSERVER=1,",0x00;+PORT+END
_DRV_HC05_AT_CLOSE_TCP_SERVER:		.db "AT+CIPSERVER=0",0x0d,0x0a,0x00,0x00
_DRV_HC05_AT_SET_TCP_CLIENT1:			.db "AT+CIPSTART=0,",0x22,"TCP",0x22,",",0x22,0x00;+IP+
_DRV_HC05_AT_SET_TCP_CLIENT2:			.db 0x22,",",0x00,0x00;+PORT+
_DRV_HC05_AT_SET_TCP_CLIENT3:			.db ",5",0x0d,0x0a,0x00,0x00
_DRV_HC05_AT_CLOSE_TCP:					.db "AT+CIPCLOSE=0",0x0d,0x0a,0x00

_DRV_HC05_AT_RECV:						.db "AT+CIPRECVDATA=0,40",0x0d,0x0a,0x00
_DRV_HC05_AT_SEND:						.db "AT+CIPSEND=0,",0x00;+XXX+END
_DRV_HC05_AT_END:							.db 0x0d,0x0a,0x00,0x00

_DRV_HC05_AT_RESP_OK:					.db 0x0d,0x0a,"OK",0x0d,0x0a,0x00,0x00
_DRV_HC05_AT_RESP_ERROR:				.db 0x0d,0x0a,"ERROR",0x0d,0x0a,0x00
_DRV_HC05_AT_RESP_IPD:					.db 0x0d,0x0a,"+IPD,",0x00
_DRV_HC05_AT_CONST_COLON:				.db ":",0x00
_DRV_HC05_AT_RESP_READY:				.db 0x0d,0x0a,">",0x00
;_DRV_HC05_AT_WIFI_CONNECTED:			.db 0x0d,0x0a,"WIFI CONNECTED",0x0d,0x0a,0x00

_DRV_HC05_WIFI_CONNECT_TIMEOUT:		.db 0x01,0xc9,0xc3,0x80	;TIME VAR = 30 сек.
_DRV_HC05_WIFI_READY_TIMEOUT:			.db 0x00,0x00,0x49,0xe0	;TIME VAR = 0.3 сек.
	;---

;--------------------------------------------------------
DRV_HC05_INIT:
;--------------------------------------------------------
;Инициализация
;IN: TEMP-ид драйвера UART, FLAGS-флаги
;--------------------------------------------------------
	LDI ACCUM,_DRV_HC05_VARS_SIZE+_DRV_HC05_BUFFER_SIZE
	MCALL C5_RAM_REALLOC

	STD Z+_DRV_HC05_UART_DRV_ID,TEMP
	STD Z+_DRV_HC05_FLAGS,FLAGS

	MCALL C5_READY
;--------------------------------------------------------
;Основной код, коммуникация
;--------------------------------------------------------
;IN: ACCUM-тип операции
;--------------------------------------------------------
	PUSH_Z
	PUSH TEMP

	MCALL C5_RAM_OFFSET

	CPI ACCUM,DRV_HC05_OP_INIT
	BRNE PC+0x04
	MCALL _DRV_HC05__OP_INIT
	RJMP _DRV_HC05_PROC_END
	CPI ACCUM,DRV_HC05_OP_RAW
	BRNE PC+0x04
	MCALL _DRV_HC05__OP_RAW
	RJMP _DRV_HC05_PROC_END

_DRV_HC05_PROC_END:

	POP TEMP
	POP_Z
	RET

;--------------------------------------------------------
_DRV_HC05__OP_RAW:
;--------------------------------------------------------
;Выполнение AT команды
;IN: X-адрес на буфер, содержащий команду,
;он же для ответа, TEMP_H-длина команды
;Z-адрес на выделенную память
;OUT:TEMP_H-результат, TEMP_L-длина
;--------------------------------------------------------
	PUSH TEMP_EH
	PUSH TEMP_EL
	PUSH TEMP
	PUSH_Y

	MOV TEMP_EH,TEMP_H
	LDI TEMP_EL,(_DRV_HC05_BUFFER_SIZE-0x01)
	MOV YH,XH
	MOV YL,XL
.ifdef LOGGING_PORT
	.if LOGGING_LEVEL >= LOGGING_LVL_PNC
		MCALL _DRV_HC05__LOG_REQV
	.endif
.endif
	LDI TEMP_H,high(5000/2)
	LDI TEMP_L,low(5000/2)
	LDD TEMP,Z+_DRV_HC05_UART_DRV_ID
	MCALL C5_EXEC
	CPI TEMP_H,DRV_UART_ST_READY
	BRNE _DRV_HC05__OP_RAW__ERROR
	CPI TEMP_L,0x00
	BREQ _DRV_HC05__OP_RAW__ERROR
.ifdef LOGGING_PORT
	.if LOGGING_LEVEL >= LOGGING_LVL_PNC
		MCALL _DRV_HC05__LOG_RESP
	.endif
.endif
	LDI TEMP_H,DRV_HC05_RESULT_OK
	RJMP _DRV_HC05__OP_RAW__END
_DRV_HC05__OP_RAW__ERROR:
	LDI TEMP_H,DRV_HC05_RESULT_ERROR
_DRV_HC05__OP_RAW__END:

	POP_Y
	POP TEMP
	POP TEMP_EL
	POP TEMP_EH
	RET

;--------------------------------------------------------
_DRV_HC05_CHECK_RESP_OK:
;--------------------------------------------------------
;Проверяем на ответ содержащий 'OK'
;IN: X-адрес на буфер содержащий ответ.
;TEMP_H-код результата выполнения команды
;TEMP_L-длина данных
;OUT:TEMP_H-результат
;--------------------------------------------------------
	PUSH_Y
	PUSH TEMP

	CPI TEMP_H,DRV_HC05_RESULT_OK
	BRNE __DRV_HC05_CHECK_RESP_OK__FAIL

	PUSH_X
	ADD XL,TEMP_L
	LDI TEMP,0x00
	ADC XH,TEMP
	ST X,TEMP
	POP_X
	LDI YH,high(_DRV_HC05_AT_RESP_OK)|0x80
	LDI YL,low(_DRV_HC05_AT_RESP_OK)
	MCALL STR_FIND
	CPI TEMP_H,0xff
	BREQ __DRV_HC05_CHECK_RESP_OK__FAIL

	LDI TEMP_H,DRV_HC05_RESULT_OK
	RJMP __DRV_HC05_CHECK_RESP_OK__END
__DRV_HC05_CHECK_RESP_OK__FAIL:

	LDI TEMP_H,DRV_HC05_RESULT_ERROR
__DRV_HC05_CHECK_RESP_OK__END:
	POP TEMP
	POP_Y
	RET

;--------------------------------------------------------
_DRV_HC05__OP_INIT:
;--------------------------------------------------------
;Инициализация HC05
;IN: Z-адрес на выделенную память, TEMP_H/L-UART baudrate
;OUT:TEMP_H-RESULT
;--------------------------------------------------------
	PUSH_X
	PUSH_Y
	PUSH TEMP_L

	;SET UART
	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH
	PUSH_X
	LDI_Y _DRV_HC05_AT_SET_UART1|0x8000
	MCALL STR_TO_RAM
	PUSH_Z
	MOV ZH,XH
	MOV ZL,XL
	MCALL NUM16_TO_STR
	MOV XH,ZH
	MOV XL,ZL
	POP_Z
	LDI_Y _DRV_HC05_AT_SET_UART2|0x8000
	MCALL STR_TO_RAM
	MOV TEMP_H,XL
	SUB TEMP_H,ZL
	SUBI TEMP_H,_DRV_HC05_BUFFER_OFFSET
	POP_X
	MCALL _DRV_HC05__OP_RAW
	MCALL _DRV_HC05_CHECK_RESP_OK
	CPI TEMP_H,DRV_HC05_RESULT_OK
	BRNE __DRV_HC05__OP_INIT__END

	;ECHO OFF
	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH
	PUSH_X
	LDI_Y _DRV_HC05_AT_ECHO_OFF|0x8000
	MCALL STR_TO_RAM
	MOV TEMP_H,XL
	SUB TEMP_H,ZL
	SUBI TEMP_H,_DRV_HC05_BUFFER_OFFSET
	POP_X
	MCALL _DRV_HC05__OP_RAW
	MCALL _DRV_HC05_CHECK_RESP_OK

__DRV_HC05__OP_INIT__END:
	POP TEMP_L
	POP_Y
	POP_X
	RET

;--------------------------------------------------------
_DRV_HC05__OP_SET_WIFI:
;--------------------------------------------------------
;Задаем подключение к WI-FI точке доступа
;IN; Z-адрес выделенной памяти
;X-адрес на имя AP, Y-адрес на пароль
;OUT:TEMP_H-RESULT
;--------------------------------------------------------
	PUSH_X
	PUSH_Y
	PUSH TEMP_EH
	PUSH TEMP_EL
	PUSH TEMP_L
	PUSH TEMP
	PUSH ACCUM
	PUSH TRY_CNTR

	PUSH_Y
	PUSH_X
	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH
	LDI YH,high(_DRV_HC05_AT_SET_WIFI1)|0x80
	LDI YL,low(_DRV_HC05_AT_SET_WIFI1)
	MCALL STR_TO_RAM
	POP_Y
	MCALL STR_TO_RAM
	LDI_Y _DRV_HC05_AT_SET_WIFI2|0x8000
	MCALL STR_TO_RAM
	POP_Y
	MCALL STR_TO_RAM
	LDI_Y _DRV_HC05_AT_SET_WIFI3|0x8000
	MCALL STR_TO_RAM
	MOV TEMP_EH,XL
	SUB TEMP_EH,ZL
	SUBI TEMP_EH,_DRV_HC05_BUFFER_OFFSET

	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH
	LDI TEMP_EL,(_DRV_HC05_BUFFER_SIZE-0x01)
	LDI TEMP_H,high(3000/2)
	LDI TEMP_L,low(3000/2)
	MOV YH,XH
	MOV YL,XL
.ifdef LOGGING_PORT
	.if LOGGING_LEVEL >= LOGGING_LVL_PNC
		MCALL _DRV_HC05__LOG_REQV
	.endif
.endif
	LDD TEMP,Z+_DRV_HC05_UART_DRV_ID
	MCALL C5_EXEC
	;Если ответ не получен - выходим с ошибкой
	CPI TEMP_H,DRV_UART_ST_READY
	BREQ PC+0x02
	RJMP __DRV_HC05__OP_SET_WIFI__FAIL
	CPI TEMP_L,0x00
	BRNE PC+0x02
	RJMP __DRV_HC05__OP_SET_WIFI__FAIL
.ifdef LOGGING_PORT
	.if LOGGING_LEVEL >= LOGGING_LVL_PNC
		MCALL _DRV_HC05__LOG_RESP
	.endif
.endif
	;Считываем сообщения ожидая "OK" в течении 15 секунд
	LDI TEMP,_DRV_HC05_TIME_START
	MCALL C5_TIME32_MARK
	RJMP __DRV_HC05__OP_SET_WIFI__FIRST_READ_SKIP
__DRV_HC05__OP_SET_WIFI__READ_LOOP:

	;Только читаем
	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH
	MOV YH,XH
	MOV YL,XL
	LDI TEMP_EH,0x00
	LDI TEMP_EL,(_DRV_HC05_BUFFER_SIZE-0x01)
	LDI TEMP_H,high(3000/2)
	LDI TEMP_L,low(3000/2)
	LDD TEMP,Z+_DRV_HC05_UART_DRV_ID
	MCALL C5_EXEC
	CPI TEMP_H,DRV_UART_ST_READY
	BREQ PC+0x02
	RJMP __DRV_HC05__OP_SET_WIFI__NO_ANSWER
	CPI TEMP_L,0x00
	BRNE PC+0x02
	RJMP __DRV_HC05__OP_SET_WIFI__NO_ANSWER
.ifdef LOGGING_PORT
	.if LOGGING_LEVEL >= LOGGING_LVL_PNC
		MCALL _DRV_HC05__LOG_RESP
	.endif
.endif
__DRV_HC05__OP_SET_WIFI__FIRST_READ_SKIP:
	PUSH_X
	ADD XL,TEMP_L
	LDI TEMP,0x00
	ADC XH,TEMP
	ST X,TEMP
	POP_X

	LDI_Y _DRV_HC05_AT_RESP_OK|0x8000
	MCALL STR_FIND
	CPI TEMP_H,0xff
	BRNE __DRV_HC05__OP_SET_WIFI__SUCCESS
	LDI_Y _DRV_HC05_AT_RESP_ERROR|0x8000
	MCALL STR_FIND
	CPI TEMP_H,0xff
	BRNE __DRV_HC05__OP_SET_WIFI__FAIL

__DRV_HC05__OP_SET_WIFI__NO_ANSWER:
	LDI ACCUM,PRIM_INT
	LDI TEMP_H,_DRV_HC05_TIME_DELTA
	LDI TEMP_L,_DRV_HC05_TIME_START
	MCALL PRIM_MOV
	LDI TEMP,_DRV_HC05_TIME_DELTA
	MCALL C5_TIME32_DELTA
	LDI_Y _DRV_HC05_WIFI_CONNECT_TIMEOUT|0x8000
	MCALL PRIM_CPI
	BRCC PC+0x02
	RJMP __DRV_HC05__OP_SET_WIFI__READ_LOOP
	RJMP __DRV_HC05__OP_SET_WIFI__FAIL
__DRV_HC05__OP_SET_WIFI__SUCCESS:

	;GET IP
	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH
	PUSH_X
	LDI_Y _DRV_HC05_AT_GET_IP|0x8000
	MCALL STR_TO_RAM
	MOV TEMP_H,XL
	SUB TEMP_H,ZL
	SUBI TEMP_H,_DRV_HC05_BUFFER_OFFSET
	POP_X
	MCALL _DRV_HC05__OP_RAW
	LDI TEMP_H,DRV_HC05_RESULT_OK
	RJMP __DRV_HC05__OP_SET_WIFI__END
__DRV_HC05__OP_SET_WIFI__FAIL:
	LDI TEMP_H,DRV_HC05_RESULT_ERROR
__DRV_HC05__OP_SET_WIFI__END:

	POP TRY_CNTR
	POP ACCUM
	POP TEMP
	POP TEMP_L
	POP TEMP_EL
	POP TEMP_EH
	POP_Y
	POP_X
	RET

;--------------------------------------------------------
_DRV_HC05__OP_TCP_SERVER:
;--------------------------------------------------------
;Активируем TCP сервер
;IN; TEMP_H/L-TCP порт
;OUT:TEMP_H-RESULT
;--------------------------------------------------------
	PUSH_X
	PUSH_Y
	PUSH TEMP_EH
	PUSH TEMP_EL
	PUSH TEMP_L

	PUSH TEMP_H
	PUSH TEMP_L

	;SET MULTIPLE CONNECTIONS
	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH
	PUSH_X
	LDI_Y _DRV_HC05_AT_SET_MULTIPLE_CONNS|0x8000
	MCALL STR_TO_RAM
	MOV TEMP_H,XL
	SUB TEMP_H,ZL
	SUBI TEMP_H,_DRV_HC05_BUFFER_OFFSET
	POP_X
	MCALL _DRV_HC05__OP_RAW
	MCALL _DRV_HC05_CHECK_RESP_OK
	CPI TEMP_H,DRV_HC05_RESULT_OK
	BREQ PC+0x02
	RJMP _DRV_HC05__OP_TCP_SERVER_ERROR1

	;SET RECIEVE MODE (CAN NOT SUPORTED)
	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH
	PUSH_X
	LDI_Y _DRV_HC05_AT_SET_RECV_MODE|0x8000
	MCALL STR_TO_RAM
	MOV TEMP_H,XL
	SUB TEMP_H,ZL
	SUBI TEMP_H,_DRV_HC05_BUFFER_OFFSET
	POP_X
	MCALL _DRV_HC05__OP_RAW

	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH
	LDI_Y _DRV_HC05_AT_CREATE_TCP_SERVER|0x8000
	MCALL STR_TO_RAM
	POP TEMP_L
	POP TEMP_H
	PUSH_Z
	MOV ZH,XH
	MOV ZL,XL
	MCALL NUM16_TO_STR
	MOV XH,ZH
	MOV XL,ZL
	POP_Z
	LDI_Y _DRV_HC05_AT_END|0x8000
	MCALL STR_TO_RAM
	MOV TEMP_H,XL
	SUB TEMP_H,ZL
	SUBI TEMP_H,_DRV_HC05_BUFFER_OFFSET
	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH
	MCALL _DRV_HC05__OP_RAW
	MCALL _DRV_HC05_CHECK_RESP_OK
	CPI TEMP_H,DRV_HC05_RESULT_OK
	BRNE _DRV_HC05__OP_TCP_SERVER_ERROR2

	;SET SERVER TIMEOUT
	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH
	PUSH_X
	LDI_Y _DRV_HC05_AT_SET_SERVER_TIMEOUT|0x8000
	MCALL STR_TO_RAM
	MOV TEMP_H,XL
	SUB TEMP_H,ZL
	SUBI TEMP_H,_DRV_HC05_BUFFER_OFFSET
	POP_X
	MCALL _DRV_HC05__OP_RAW

	LDI TEMP_H,DRV_HC05_RESULT_OK
	RJMP _DRV_HC05__OP_TCP_SERVER_END
_DRV_HC05__OP_TCP_SERVER_ERROR1:
	POP TEMP_L
	POP TEMP_H
_DRV_HC05__OP_TCP_SERVER_ERROR2:
	LDI TEMP_H,DRV_HC05_RESULT_ERROR
_DRV_HC05__OP_TCP_SERVER_END:
	POP TEMP_L
	POP TEMP_EL
	POP TEMP_EH
	POP_Y
	POP_X
	RET

;--------------------------------------------------------
_DRV_HC05__OP_RECEIVE_DATA:
;--------------------------------------------------------
;Принимаем данные
;IN: Z-адрес на выделенную память, TEMP_H/L-таймаут
;ожидания данных (2мс)
;OUT:TEMP_H-результат, TEMP_L-длина,Y-адрес на начало
;данных
;--------------------------------------------------------
	PUSH_X
	PUSH TEMP_EH
	PUSH TEMP_EL

	;Только читаем
	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH
	MOV YH,XH
	MOV YL,XL
	LDI TEMP_EH,0x00
	LDI TEMP_EL,(_DRV_HC05_BUFFER_SIZE-0x01)
	LDD TEMP,Z+_DRV_HC05_UART_DRV_ID
	MCALL C5_EXEC

	CPI TEMP_H,DRV_UART_ST_READY
	BREQ PC+0x02
	RJMP _DRV_HC05__OP_RECEIVE_DATA__NO_ANSWER
	CPI TEMP_L,0x00
	BRNE PC+0x02
	RJMP _DRV_HC05__OP_RECEIVE_DATA__NO_ANSWER
	PUSH_X
	ADD XL,TEMP_L
	LDI TEMP,0x00
	ADC XH,TEMP
	ST X,TEMP
	POP_X
	LDI_Y _DRV_HC05_AT_RESP_IPD|0x8000
	MOV TEMP_EL,TEMP_L
	MCALL STR_FIND
	CPI TEMP_H,0xff
	BREQ _DRV_HC05__OP_RECEIVE_DATA__ERROR
	SUB TEMP_EL,TEMP_L
	ADD XL,TEMP_L
	CLR TEMP
	ADC XH,TEMP

	LDI_Y _DRV_HC05_AT_CONST_COLON|0x8000
	MCALL STR_FIND
	CPI TEMP_H,0xff
	BREQ _DRV_HC05__OP_RECEIVE_DATA__ERROR
	SUB TEMP_EL,TEMP_L
	ADD XL,TEMP_L
	CLR TEMP
	ADC XH,TEMP
	MOV TEMP_L,TEMP_EL

	MOV YH,XH
	MOV YL,XL
	LDI TEMP_H,DRV_HC05_RESULT_OK
	RJMP _DRV_HC05__OP_RECEIVE_DATA__END
_DRV_HC05__OP_RECEIVE_DATA__NO_ANSWER:
	LDI TEMP_H,DRV_HC05_RESULT_NO_ANSWER
	RJMP _DRV_HC05__OP_RECEIVE_DATA__END
_DRV_HC05__OP_RECEIVE_DATA__ERROR:
	LDI TEMP_H,DRV_HC05_RESULT_ERROR
	RJMP _DRV_HC05__OP_RECEIVE_DATA__END
_DRV_HC05__OP_RECEIVE_DATA__END:
	POP TEMP_EL
	POP TEMP_EH
	POP_X
	RET

;--------------------------------------------------------
_DRV_HC05__OP_SEND_DATA:
;--------------------------------------------------------
;Передаем данные данные
;IN: Z-адрес на выделенную память, Y-адрес на начало
;передаваемых данных, TEMP_L-размер данных
;OUT:TEMP_H-результат
;--------------------------------------------------------
	PUSH_X
	PUSH_Y
	PUSH TEMP
	PUSH TEMP_L
	PUSH TEMP_EH
	PUSH TEMP_EL

	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH

	PUSH TEMP_L
	PUSH_X
	PUSH_Y
	LDI_Y _DRV_HC05_AT_SEND|0x8000
	MCALL STR_TO_RAM
	POP_Y
	PUSH_Z
	MOV ZH,XH
	MOV ZL,XL
	MOV TEMP,TEMP_L
	LDI TEMP_H,0x00
	MCALL NUM16_TO_STR
	MOV XH,ZH
	MOV XL,ZL
	POP_Z
	PUSH_Y
	LDI_Y _DRV_HC05_AT_END|0x8000
	MCALL STR_TO_RAM
	POP_Y
	MOV TEMP_H,XL
	SUB TEMP_H,ZL
	SUBI TEMP_H,_DRV_HC05_BUFFER_OFFSET
	POP_X
	MCALL _DRV_HC05__OP_RAW
	MCALL _DRV_HC05_CHECK_RESP_OK
	CPI TEMP_H,DRV_HC05_RESULT_OK
	POP TEMP_EH
	BREQ PC+0x02
	RJMP __DRV_HC05__OP_SEND_DATA__END


	;Считываем сообщения ожидая ">" в течении 0.3 секунд
	LDI TEMP,_DRV_HC05_TIME_START
	MCALL C5_TIME32_MARK
__DRV_HC05__OP_SEND_DATA__READ_LOOP:

	;Только читаем
	LDI XH,0x00
	LDI XL,_DRV_HC05_BUFFER_OFFSET
	ADD XL,ZL
	ADC XH,ZH
	PUSH_Y
	MOV YH,XH
	MOV YL,XL
	LDI TEMP_EH,0x00
	LDI TEMP_EL,(_DRV_HC05_BUFFER_SIZE-0x01)
	LDI TEMP_H,high(300/2)
	LDI TEMP_L,low(300/2)
	LDD TEMP,Z+_DRV_HC05_UART_DRV_ID
	MCALL C5_EXEC
	POP_Y
	CPI TEMP_H,DRV_UART_ST_READY
	BREQ PC+0x02
	RJMP __DRV_HC05__OP_SEND_DATA__NO_ANSWER
	CPI TEMP_L,0x00
	BRNE PC+0x02
	RJMP __DRV_HC05__OP_SEND_DATA__NO_ANSWER
	PUSH_X
	ADD XL,TEMP_L
	LDI TEMP,0x00
	ADC XH,TEMP
	ST X,TEMP
	POP_X

	LDI_Y _DRV_HC05_AT_RESP_READY|0x8000
	MCALL STR_FIND
	CPI TEMP_H,0xff
	BRNE __DRV_HC05__OP_SEND_DATA__SUCCESS

__DRV_HC05__OP_SEND_DATA__NO_ANSWER:
	LDI ACCUM,PRIM_INT
	LDI TEMP_H,_DRV_HC05_TIME_DELTA
	LDI TEMP_L,_DRV_HC05_TIME_START
	MCALL PRIM_MOV
	LDI TEMP,_DRV_HC05_TIME_DELTA
	MCALL C5_TIME32_DELTA
	LDI_Y _DRV_HC05_WIFI_READY_TIMEOUT|0x8000
	MCALL PRIM_CPI
	BRCC PC+0x02
	RJMP __DRV_HC05__OP_SEND_DATA__READ_LOOP
	RJMP __DRV_HC05__OP_SEND_DATA__ERROR
__DRV_HC05__OP_SEND_DATA__SUCCESS:

	LDI TEMP_EL,0x00;(_DRV_HC05_BUFFER_SIZE-0x01)
.ifdef LOGGING_PORT
	.if LOGGING_LEVEL >= LOGGING_LVL_PNC
		MCALL _DRV_HC05__LOG_REQV_BYTES
	.endif
.endif
	LDI TEMP_H,high(1000/2)
	LDI TEMP_L,low(1000/2)
	LDD TEMP,Z+_DRV_HC05_UART_DRV_ID
	MCALL C5_EXEC
	CPI TEMP_H,DRV_HC05_RESULT_OK
	BRNE __DRV_HC05__OP_SEND_DATA__ERROR
.ifdef LOGGING_PORT
	.if LOGGING_LEVEL >= LOGGING_LVL_PNC
		MOV YH,XH
		MOV YL,XL
		MCALL _DRV_HC05__LOG_RESP
	.endif
.endif

	;TODO CHECK 'SEND OK'

	CPI TEMP_H,DRV_UART_ST_READY
	BRNE __DRV_HC05__OP_SEND_DATA__ERROR
	LDI TEMP_H,DRV_HC05_RESULT_OK
	RJMP __DRV_HC05__OP_SEND_DATA__END
__DRV_HC05__OP_SEND_DATA__ERROR:
	LDI TEMP_H,DRV_HC05_RESULT_ERROR
__DRV_HC05__OP_SEND_DATA__END:

	POP TEMP_EL
	POP TEMP_EH
	POP TEMP_L
	POP TEMP
	POP_Y
	POP_X
	RET



_DRV_HC05__OP_TCP_CLOSE:
	;TODO
	RET

.ifdef LOGGING_PORT
.if LOGGING_LEVEL >= LOGGING_LVL_PNC
;--------------------------------------------------------
_DRV_HC05__LOG_REQV:
;--------------------------------------------------------
;Внутренняя процедура логирования
;--------------------------------------------------------
	LDD TEMP,Z+_DRV_HC05_FLAGS
	SBRS TEMP,DRV_HC05_FLAG_LOGGING
	RET
	LDI TEMP,'>'
	MCALL C5_OUT_CHAR
	MOV TEMP,TEMP_EH
	PUSH_Y
	MCALL STRN_TRIM
	MCALL C5_OUT_STRN
	POP_Y
	MCALL C5_OUT_CR
	RET
;--------------------------------------------------------
_DRV_HC05__LOG_REQV_BYTES:
;--------------------------------------------------------
;Внутренняя процедура логирования
;--------------------------------------------------------
	LDD TEMP,Z+_DRV_HC05_FLAGS
	SBRS TEMP,DRV_HC05_FLAG_LOGGING
	RET
	LDI TEMP,'>'
	MCALL C5_OUT_CHAR
	MOV TEMP,TEMP_EH
	MCALL C5_OUT_BYTES
	MCALL C5_OUT_CR
	RET
;--------------------------------------------------------
_DRV_HC05__LOG_RESP:
;--------------------------------------------------------
;Внутренняя процедура логирования
;--------------------------------------------------------
	LDD TEMP,Z+_DRV_HC05_FLAGS
	SBRS TEMP,DRV_HC05_FLAG_LOGGING
	RET
	LDI TEMP,'<'
	MCALL C5_OUT_CHAR
	MOV TEMP,TEMP_L
	PUSH_Y
	MCALL STRN_TRIM
	MCALL C5_OUT_STRN
	POP_Y
	MCALL C5_OUT_CR
	RET
.endif
.endif
.endif
