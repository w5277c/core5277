;-----------------------------------------------------------------------------------------------------------------------
;Владельцем данного исходного кода является Удовиченко Константин Александрович, емайл:w5277c@gmail.com, по всем
;правовым вопросам обращайтесь на email.
;-----------------------------------------------------------------------------------------------------------------------
;28.05.2020  w5277c@gmail.com        Начало
;01.08.2020  w5277c@gmail.com        Разбиение на файлы
;-----------------------------------------------------------------------------------------------------------------------
.ifdef DEF_CORE5277_RAM_CHECK_STACK
.else
.set DEF_CORE5277_RAM_CHECK_STACK = 1

;--------------------------------------------------------
_CORE5277_RAM_CHECK_STACK:
;--------------------------------------------------------
;Проверяем на влияние на стек
;IN: Y - адрес блока, ACCUM - длина
;OUT: flag Z(true - влияния нет)
;--------------------------------------------------------
	PUSH_X
	PUSH_Y
	PUSH ACCUM

	;Считаем конец блока
	ADD YL,ACCUM
	CLR ACCUM
	ADC YH,ACCUM
	;Получаем адрес вершины стека
	LDS XH,_CORE5277_TOP_OF_STACK+0x00
	LDS XL,_CORE5277_TOP_OF_STACK+0x01
	;Находим разницу
	SUB XL,YL
	SBC XH,YH
	BRCS _CORE5277_RAM_CHECK_STACK__FAIL
	;Учитываем минимально допустимый размер
	SUBI XL,_CORE5277_RAM_BORDER_SIZE
	SBCI XH,0x00
	BRCS _CORE5277_RAM_CHECK_STACK__FAIL
	SEZ
	RJMP _CORE5277_RAM_CHECK_STACK__END
_CORE5277_RAM_CHECK_STACK__FAIL:
	CLZ
_CORE5277_RAM_CHECK_STACK__END:
	POP ACCUM
	POP_Y
	POP_X
	RET
.endif
