;-----------------------------------------------------------------------------------------------------------------------
;Владельцем данного исходного кода является Удовиченко Константин Александрович, емайл:w5277c@gmail.com, по всем
;правовым вопросам обращайтесь на email.
;-----------------------------------------------------------------------------------------------------------------------
;07.10.2019  w5277c@gmail.com        Начало
;01.08.2020  w5277c@gmail.com        Разбиение на файлы
;-----------------------------------------------------------------------------------------------------------------------
.ifdef DEF_CORE5277_DIV16X8
.else
.set DEF_CORE5277_DIV16X8 = 1

;--------------------------------------------------------
CORE5277_DIV16X8:
;--------------------------------------------------------
;Деление 16b числа на 8б число
;IN: TEMP_H,TEMP_L - 16b число, TEMP - 8b число
;OUT: TEMP_H, TEMP_L - 16b результат
;-START--------------------------------------------------
	PUSH_X
	PUSH LOOP_CNTR

	CLR XH
	CLR XL
	INC XL
	CLR LOOP_CNTR

_CORE5277_DIV16X8__A:
	CLC
	ROL TEMP_L
	ROL TEMP_H
	ROL LOOP_CNTR
	BRCS _CORE5277_DIV16X8__B
	CP LOOP_CNTR,TEMP
	BRCS _CORE5277_DIV16X8__C
_CORE5277_DIV16X8__B:
	SUB LOOP_CNTR,TEMP
	SEC
	RJMP _CORE5277_DIV16X8__D
_CORE5277_DIV16X8__C:
	CLC
_CORE5277_DIV16X8__D:
	ROL XL
	ROL XH
	BRCC _CORE5277_DIV16X8__A
	MOV TEMP_H,XH
	MOV TEMP_L,XL

   POP LOOP_CNTR
	POP_X
	RET
.endif
